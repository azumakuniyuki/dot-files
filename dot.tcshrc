# $Id: dot.tcshrc,v 1.127 2009/09/18 06:48:57 ak Exp $
#                                                       
#           ##   ##               ##                    
#  ###     ### ###### ####  ##### ##     #####   ####   
#    #### ###    ##  ##    ##     #####  ##  ## ##      
#        ###     ##  ##     ####  ##  ## ##     ##      
#       ###   ## ##  ##        ## ##  ## ##     ##      
#       ##    ##  ### #### #####  ##  ## ##      ####   
#
# ~/.tcshrc                             ( symbolic link )
# ~/share/dot-files/dot.tcshrc          ( this file )
# ~/share/dot-files/dot.tcshrc-complete ( command complements )
# ~/share/dot-files/dot.tcsh-aliases.*  ( command aliases )
# ~/share/dot-files/dot.tcsh-x11        ( x11 settings )
# ~/share/hosts-and-users/host.*        ( hostnames for command complements )
# ~/share/hosts-and-users/addr.*        ( IP addresses for command complements )
# ~/share/hosts-and-users/user.*        ( usernames for command complements )
# ~/.cvsroot                            ( for CVSROOT env.varialble )
# ~/.myaddr                             ( contains my mail address )
# ~/.ssh-agent                          ( ssh-agent boot switch )
#
############################################################################
#  ____ ____ ____ ____ ____ _________ ____ ____ ____ ____ ____ ____ ____ ____ ____ 
# ||s |||h |||e |||l |||l |||       |||v |||a |||r |||i |||a |||b |||l |||e |||s ||
# ||__|||__|||__|||__|||__|||_______|||__|||__|||__|||__|||__|||__|||__|||__|||__||
# |/__\|/__\|/__\|/__\|/__\|/_______\|/__\|/__\|/__\|/__\|/__\|/__\|/__\|/__\|/__\|
#
if( -d "${HOME}/share" ) then
    unset conf_tcsh conf_name
    if( -d "${HOME}/share/dot-files" ) set conf_tcsh = "${HOME}/share/dot-files"
    if( -d "${HOME}/share/hosts-and-users" ) set conf_name = "${HOME}/share//hosts-and-users"
endif
umask 022

#  ____ ____ ____ ____ ____ ____ ____ _________ ____ ____ ____ ____ 
# ||c |||o |||m |||m |||a |||n |||d |||       |||p |||a |||t |||h ||
# ||__|||__|||__|||__|||__|||__|||__|||_______|||__|||__|||__|||__||
# |/__\|/__\|/__\|/__\|/__\|/__\|/__\|/_______\|/__\|/__\|/__\|/__\|
#
set path = ()
set directories_in_usr = ( bin sbin games ucb X11R6/bin )
set directories_in_home = ( script bin sbin opt/bin opt/sbin )
set directories_in_local = ( \
    teTeX ghostscript sendmail pgsql postgres mysql sqlite named \
    apache httpd2 uucp openssl clamXav clamav subversion git mercurial )

foreach __DIH ( $directories_in_home )
    if( -d "${HOME}/${__DIH}" ) set path = ( $path ${HOME}/${__DIH} )
end

foreach __DIL ( $directories_in_local )
    if( -d "/usr/local/${__DIL}/bin" ) set path = ( $path /usr/local/${__DIL}/bin )
    if( -d "/usr/local/${__DIL}/sbin" ) set path = ( $path /usr/local/${__DIL}/sbin )
end

foreach __DIU ( $directories_in_usr )
    if( -d "/usr/local/${__DIU}" ) set path = ( $path /usr/local/${__DIU} )
    if( -d "/usr/${__DIU}" ) set path = ( $path /usr/${__DIU} )
    if( -d "/opt/local/${__DIU}" ) set path = ( $path /opt/local/${__DIU} )
    if( -d "/opt/${__DIU}" ) set path = ( $path /opt/${__DIU} )
    if( -d "/${__DIU}" ) set path = ( $path /${__DIU} )
end
unset __DIH __DIL __DIU


#  ____ ____ ____ ____ ____ _________ ____ ____ ____ ____ ____ 
# ||l |||o |||g |||i |||n |||       |||s |||h |||e |||l |||l ||
# ||__|||__|||__|||__|||__|||_______|||__|||__|||__|||__|||__||
# |/__\|/__\|/__\|/__\|/__\|/_______\|/__\|/__\|/__\|/__\|/__\|
#
if($?prompt) then

    stty erase  "^?"
    stty kill   "^U"
    stty intr   "^C"

    set vioptions = "-U /dev/null -u /dev/null -c 'set ai nu sm ts=8'"
    if( -x '/usr/bin/vi' ) then
        setenv EDITOR       '/usr/bin/vi'
        setenv VISUAL       '/usr/bin/vi'
    else if( -x '/bin/vi' ) then
        setenv EDITOR       '/bin/vi'
        setenv VISUAL       '/bin/vi'
    else
        setenv EDITOR       vi
        setenv VISUAL       vi
    endif

    setenv EDITOR       "$EDITOR $vioptions"
    setenv VISUAL       "$VISUAL $vioptions"

    setenv DISPLAY      '127.0.0.1:0.0'
    setenv PAGER        'less'
    setenv BLOCKSIZE    '1k'
    setenv PERL_BADLANG 0

    setenv TERM     'xterm'
    # setenv TERM   vt100
    # setenv TERM   vt220
    # setenv TERM   ansi
    # setenv TERM   cons25

    set addsuffix
    set noclobber
    set notify
    set filec
    set nobeep
    set history = 4096
    set savehist    = 8192
    set autolist
    set autoexpand
    unset cdpath
    #set mail   = (600 $HOME/Maildir/cur)

    limit coredumpsize 0

    #  __  __    _    _   _ ____   _  _____ _   _ 
    # |  \/  |  / \  | \ | |  _ \ / \|_   _| | | |
    # | |\/| | / _ \ |  \| | |_) / _ \ | | | |_| |
    # | |  | |/ ___ \| |\  |  __/ ___ \| | |  _  |
    # |_|  |_/_/   \_\_| \_|_| /_/   \_\_| |_| |_|
    # 
    setenv MANPATH '/man'
    foreach manpath ( man share/man local/man local/share/man X11R6/man X11R6/share/man )
        if( -d "/usr/$manpath" ) setenv MANPATH "${MANPATH}:/usr/${manpath}"
    end

    foreach localman ( ${directories_in_local} )
        if( -d "/usr/local/${localman}/man" ) setenv MANPATH "${MANPATH}:/usr/local/${localman}/man"
        if( -d "/usr/local/${localman}/share/man" ) setenv MANPATH "${MANPATH}:/usr/local/${localman}/share/man"
    end
    unset manpath localman

    #   ___  ____ _______   ______  _____ 
    #  / _ \/ ___|_   _\ \ / /  _ \| ____|
    # | | | \___ \ | |  \ V /| |_) |  _|  
    # | |_| |___) || |   | | |  __/| |___ 
    #  \___/|____/ |_|   |_| |_|   |_____|
    # 
    if( ! "$?OSTYPE" ) then
        if( "$?HOSTTYPE" ) then
            setenv OSTYPE "$HOSTTYPE"
        else if( "$?OS" ) then
            setenv OSTYPE "$OS"
        else
            setenv OSTYPE "`uname -s`"
        endif
    endif

    #  ____  _____ __  __  ___ _____ _____ _   _  ___  ____ _____ 
    # |  _ \| ____|  \/  |/ _ \_   _| ____| | | |/ _ \/ ___|_   _|
    # | |_) |  _| | |\/| | | | || | |  _| | |_| | | | \___ \ | |  
    # |  _ <| |___| |  | | |_| || | | |___|  _  | |_| |___) || |  
    # |_| \_\_____|_|  |_|\___/ |_| |_____|_| |_|\___/|____/ |_|  
    #                                                             
    if( ! "$?REMOTEHOST" ) then
        if( "$?SSH_CLIENT" ) then
            setenv REMOTEHOST "`printenv SSH_CLIENT | awk '{print $1}'`"
        else
            setenv REMOTEHOST 'localhost'
        endif
    endif

    #  _        _    _   _  ____ _   _   _    ____ _____ 
    # | |      / \  | \ | |/ ___| | | | / \  / ___| ____|
    # | |     / _ \ |  \| | |  _| | | |/ _ \| |  _|  _|  
    # | |___ / ___ \| |\  | |_| | |_| / ___ \ |_| | |___ 
    # |_____/_/   \_\_| \_|\____|\___/_/   \_\____|_____|
    #                                                    
    setenv SJIS     'ja_JP.SJIS'
    setenv EUC      'ja_JP.eucJP'
    setenv UJIS     'ja_JP.UJIS'
    setenv JP_UTF8  'ja_JP.UTF-8'

    setenv LANG         $JP_UTF8
    setenv LC_MESSAGES  'C'
    setenv LC_TIME      'C'
    setenv LC_CTYPE     $JP_UTF8
    setenv JLESSCHARSET 'japanese-sjis'

    #   ___ _____ _   _ _____ ____  ____  
    #  / _ \_   _| | | | ____|  _ \/ ___| 
    # | | | || | | |_| |  _| | |_) \___ \ 
    # | |_| || | |  _  | |___|  _ < ___) |
    #  \___/ |_| |_| |_|_____|_| \_\____/ 
    #                                     
    if( -e "$HOME/.cvsroot" ) then
        setenv CVSROOT      "`head -n 1 $HOME/.cvsroot`"
        setenv CVS_RSH      'ssh'
        setenv CVSEDITOR    "$EDITOR"
    endif

    if( -e "$HOME/.myaddr" ) setenv MYADDR "`head -n 1 $HOME/.myaddr`"
    if( -x "`which rsync`" ) setenv RSYNC_RSH "ssh"

    setenv FTPMODE      'passive'


    ## tcsh as login shell #############################################
    if ( "$?tcsh" ) then
        #  ____  ____   ___  __  __ ____ _____ 
        # |  _ \|  _ \ / _ \|  \/  |  _ \_   _|
        # | |_) | |_) | | | | |\/| | |_) || |  
        # |  __/|  _ <| |_| | |  | |  __/ | |  
        # |_|   |_| \_\\___/|_|  |_|_|    |_|  
        #                                      
        ## set prompt(tcsh) ########################################
        #
        # Bb    : bold
        # C,c,. : `basename $cwd`
        # D     : `date "+%d"`
        # d     : `date "+%a"`
        # h,!   : history number
        # j     : ...
        # l     : `basename `tty``
        # M     : `hostname`
        # m     : `hostname -s`
        # n     : `logname`
        # P     : `date '+%T`
        # p     : `date '+%T%p`
        # Ss    : turn over
        # T     : HH:mm
        # t,@   : HH:mm`date '+%p'`
        # Uu    : underline
        # W     : month number
        # w     : month name(short)
        # Y     : year number
        # y     : year number(short)
        # #     : # or >
        # %     : %
        # /,~   : $cwd
        # ?     : $?
        #
        ## smiley prompt ###########################################
        #
        # (^_^) : $status = 0
        # (>_<) : $status = 1
        #
        if( "$USER" != 'root' ) then
            # alias cwdcmd 'if( ! $status ) set prompt = "%U%B%m:%l%u:%b\^\_\^\)\/%B%~ \> %b" '
            # alias precmd 'if( $status ) set prompt = "%U%B%m:%l%u:%b\/\(\>\_\<;%B%~ \> %b" '
            # set prompt = "%U%B%m:%l%u:%b\'0\'\)%B%~ \> %b"

            # no bold prompt
            # alias cwdcmd 'if( ! $status ) set prompt = "%U%m:%l%u:\^\_\^\)\/%B%~ \> %b" '
            # alias precmd 'if( $status ) set prompt = "%U%m:%l%u:\/\(\>\_\<;%B%~ \> %b" '
            # set prompt = "%U%m:%l%u:%b\'o\'\)%B%~ \> "
            set prompt = "%U%n@%m\(%l\):%B%c%b\(%h\)%u %# "
        else
            set prompt = "%m@%~ % "
        endif

        # set prompt    = "%B%m::\(\-\_\-\;%~ % %b"
        # set prompt    = "%B%m::\\(\^\o\^\)\/%~ % %b"
        # set prompt    = "%B%m::\(\T\o\T\;%~ % %b"
        # set prompt    = "%B%m::%~ % %b"
        # set prompt    = "%m@%~ % "
        # set prompt    = "%c (%B%m%b:\!) % "
        # set rprompt   = '%t'

        set fignore = ( .swp ,v .bak .o .out .dvi .aux .toc \~ )
        set watch   = ( 1 any any )
        set rmstar
        # set printexitvalue
        unset ampm autologout autocorrect

        # set multibyte = sjis
        # set color
        # set watch = ( 1 any console )
        # set dspmbyte  = sjis 
        # set dspkanji  = sjis
        set dspmbyte    = utf8

        #  _   _ _____ _____ ____    _   _ ____  _____ ____      _    ____ _____ _   _ _____ 
        # | | | |_   _|_   _|  _ \  | | | / ___|| ____|  _ \    / \  / ___| ____| \ | |_   _|
        # | |_| | | |   | | | |_) | | | | \___ \|  _| | |_) |  / _ \| |  _|  _| |  \| | | |  
        # |  _  | | |   | | |  __/  | |_| |___) | |___|  _ <  / ___ \ |_| | |___| |\  | | |  
        # |_| |_| |_|   |_| |_|      \___/|____/|_____|_| \_\/_/   \_\____|_____|_| \_| |_|  
        #                                                                                    
        set ua_http01   = "Mozilla/4.0 (compatible; MSIE 6.0; Windows NT 5.0; .NET CLR 1.0.3705; .NET CLR 1.1.4322)"
        set ua_http02   = "Mozilla/4.0 (compatible; MSIE 6.0; Windows NT 5.1;SV1; .NET CLR 1.0.3705; .NET CLR 1.1.4322)"
        set ua_http03   = "Opera/9.00 (Windows NT 5.1; U; en)"
        set ua_http04   = "Mozilla/5.0 (X11; U; SunOS sun4u; en-US; rv:1.8)Gecko/20051130 Firefox/1.5"
        set ua_http05   = "Mozilla/5.0 (Windows; U; Windows NT 5.2; ja-JP;rv:1.7.12) Gecko/20050919 Firefox/1.0.7"
        set ua_http10   = "DoCoMo/2.0 P900iV(c100;TB;W24H11)"
        set ua_http11   = "Vodafone"
        set ua_http12   = "KDDI-KC32 UP.Browser/6.2.0.7.3.129 (GUI) MMP/2.0"

        #   ____ ___  __  __ __  __    _    _   _ ____       _    _     ___    _    ____  
        #  / ___/ _ \|  \/  |  \/  |  / \  | \ | |  _ \     / \  | |   |_ _|  / \  / ___| 
        # | |  | | | | |\/| | |\/| | / _ \ |  \| | | | |   / _ \ | |    | |  / _ \ \___ \ 
        # | |__| |_| | |  | | |  | |/ ___ \| |\  | |_| |  / ___ \| |___ | | / ___ \ ___) |
        #  \____\___/|_|  |_|_|  |_/_/   \_\_| \_|____/  /_/   \_\_____|___/_/   \_\____/ 
        #                                                                                 
        alias ls    'ls-F -1'   # use tcsh built-in ls-F
        alias la    'ls-F -a'
        alias ll    'ls-F -la'
        alias lc    'ls-F -C'
        alias j     'jobs'
        alias h     'history 36'
        alias ssh   'ssh -v42'
        alias fwdssh    'ssh -v42 -oStrictHostKeyChecking=no'
        alias up    'cd ../;pwd'
        alias today "date '+%F'"
        alias ip4grep   "grep -E '([0-9]{1,3}\.){3}[0-9]{1,3}'"
        alias ip4rev    "echo \!* | sed 's/^\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\)/\4.\3.\2.\1/g'"
        alias wget  "wget -vnc -T 32 -U '$ua_http05'"
        alias curl  "curl -LivfO# --connect-timeout 30 -A '$ua_http02'"
        alias sqlite    "sqlite3 -header -column"
        alias fchar6    "echo \!* | tr '[A-Za-z]' '[a-zA-Z]' | figlet -fcharact6 -w192"
        alias vi    "vi $vioptions"
        alias rot13 "tr '[a-zA-Z]' '[n-za-mN-ZA-M]'"
        alias git-logx  'git log --graph --decorate --pretty=format:"%ad [%cn] <c:%h t:%t p:%p> %n %Cgreen%d%Creset %s %n" --stat -p'
        alias git-arch  'git archive'
        alias git-lsum  'git log --stat --summary'
        alias git-stat  'git status'

        ## Set hostnames and usernames ###################################
        #
        # * read files which contain hostnames and usernames for complement
        #   of network commands in the command line. The following scripts
        #   substitute hostnames and usernames for variables: $hostnames, 
        #   and $usernames.
        #
        set can_complete = 0

        #  _   _ ____  _____ ____  _   _    _    __  __ _____ ____  
        # | | | / ___|| ____|  _ \| \ | |  / \  |  \/  | ____/ ___| 
        # | | | \___ \|  _| | |_) |  \| | / _ \ | |\/| |  _| \___ \ 
        # | |_| |___) | |___|  _ <| |\  |/ ___ \| |  | | |___ ___) |
        #  \___/|____/|_____|_| \_\_| \_/_/   \_\_|  |_|_____|____/ 
        #                                                           
        if( "$?conf_name" ) then
            if( `/bin/ls -1 $conf_name/user.*` != "" ) then
                set usernames = ()
                foreach __u ( $conf_name/user.* )
                    if ( -r "$__u" ) set usernames = ( $usernames `cat $__u | egrep -v '^#|^$' | awk '{print $1 }' | sort -u ` )
                end

                set usernames = ( $usernames $USER )
                set can_complete = `expr $can_complete + 1`
                unset __u
            endif
        endif

        #  _   _  ___  ____ _____ _   _    _    __  __ _____ ____  
        # | | | |/ _ \/ ___|_   _| \ | |  / \  |  \/  | ____/ ___| 
        # | |_| | | | \___ \ | | |  \| | / _ \ | |\/| |  _| \___ \ 
        # |  _  | |_| |___) || | | |\  |/ ___ \| |  | | |___ ___) |
        # |_| |_|\___/|____/ |_| |_| \_/_/   \_\_|  |_|_____|____/ 
        #                                                          
        if( "$?conf_name" ) then
            if( `/bin/ls -1 $conf_name/` != "" ) then
                set hostnames = ()
                foreach __h ( $conf_name/host.* $conf_name/addr.* )
                    if ( -r "$__h" ) set hostnames = ( $hostnames `cat $__h | egrep -v '^#|^$' | awk '{ print $1 }' | sort -u ` )
                end

                set hostaddrs = ()
                foreach __a ( $conf_name/addr.* )
                    if ( -r "$__a" ) set hostaddrs = ( $hostaddrs `cat $__a | egrep -v '^#|^$' | awk '{ print $1 }' | sort -u ` )
                end

                set hostnames = ( $hostnames `uname -n` )
                set can_complete = `expr $can_complete + 1`
                unset __h
                unset __a
            endif
        endif

        #  _     ___    _    ____  
        # | |   / _ \  / \  |  _ \ 
        # | |  | | | |/ _ \ | | | |
        # | |__| |_| / ___ \| |_| |
        # |_____\___/_/   \_\____/ 
        #                          
        if( "$?conf_tcsh" ) then

            if( -r "$conf_tcsh/dot.tcsh-complete" && $can_complete > 0 ) then
                source $conf_tcsh/dot.tcsh-complete
            endif

            if( -r "$conf_tcsh/dot.tcsh-alias.$OSTYPE" ) then
                source $conf_tcsh/dot.tcsh-alias.$OSTYPE
            endif

            if( "$?conf_tcsh" ) then
                if( "$?DISPLAY" && -r "$conf_tcsh/dot.tcsh-x11" ) then
                    source $conf_tcsh/dot.tcsh-x11
                endif
            endif
        endif

        unset can_complete

    endif
    #
    ## end of tcsh settings ############################################

    #  ____  _____ ____ _   _ ____  _____   ____  _   _ _____ _     _     
    # / ___|| ____/ ___| | | |  _ \| ____| / ___|| | | | ____| |   | |    
    # \___ \|  _|| |   | | | | |_) |  _|   \___ \| |_| |  _| | |   | |    
    #  ___) | |__| |___| |_| |  _ <| |___   ___) |  _  | |___| |___| |___ 
    # |____/|_____\____|\___/|_| \_\_____| |____/|_| |_|_____|_____|_____|
    #                                                                     
    if( -r "${HOME}/.ssh-agent" && -r `head -n 1 ${HOME}/.ssh-agent` ) then

        set ap = (`ps x | grep -i ssh-agent | grep -v 'grep'`)

        ## import running ssh-agent pid, socket
        if( "$ap" != "" ) then
            setenv SSH_AGENT_PID "$ap[1]"
            setenv SSH_AUTH_SOCK "`find /tmp/ -user $user -name 'agent.*' -type s`"
        else
            eval `ssh-agent` >& /dev/null
            ssh-add
        endif

        unset ap
    endif

    #  _     ___   ____ ___ _   _   ____    _    _   _ _   _ _____ ____  
    # | |   / _ \ / ___|_ _| \ | | | __ )  / \  | \ | | \ | | ____|  _ \ 
    # | |  | | | | |  _ | ||  \| | |  _ \ / _ \ |  \| |  \| |  _| | |_) |
    # | |__| |_| | |_| || || |\  | | |_) / ___ \| |\  | |\  | |___|  _ < 
    # |_____\___/ \____|___|_| \_| |____/_/   \_\_| \_|_| \_|_____|_| \_\
    #                                                                    
    echo
    echo "  * LOGNAME  : $user ($uid)"
    echo "  * GROUP    : $group ($gid)"
    echo "  * FROM     : $REMOTEHOST"
    echo "  * TO       : $HOST"
    echo "  * TTY      : $tty"
    echo "  * DATE     : "`date "+%F(%a) %T" | tr "-" "/"`
    echo "  * SYSTEM   : `uname -s -r` (`uname -m`)"
    #if( "$SSH_AUTH_SOCK" != "" ) echo "  * AGENT    : $SSH_AUTH_SOCK"
    echo
    echo '_______________________________________________________________'
    echo

    #   ____ _     _____    _    _   _ _   _ ____  
    #  / ___| |   | ____|  / \  | \ | | | | |  _ \ 
    # | |   | |   |  _|   / _ \ |  \| | | | | |_) |
    # | |___| |___| |___ / ___ \| |\  | |_| |  __/ 
    #  \____|_____|_____/_/   \_\_| \_|\___/|_|    
    #                                              
    unset conf_tcsh conf_name 

endif
## end of login shell settings #############################################

